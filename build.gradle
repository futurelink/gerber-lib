plugins {
    id 'java'
    id 'maven-publish'
}

group = 'io.msla'
version = '0.1.2'

repositories {
    mavenCentral()
}

compileJava {
    options.encoding = 'UTF-8'
}

compileTestJava {
    options.encoding = 'UTF-8'
}

jar {
    archiveBaseName = 'gerber-lib'
    archiveVersion = project.version
}

dependencies {
    compileOnly 'org.projectlombok:lombok:1.18.28'
    annotationProcessor 'org.projectlombok:lombok:1.18.28'

    testCompileOnly 'org.projectlombok:lombok:1.18.28'
    testAnnotationProcessor 'org.projectlombok:lombok:1.18.28'

    testImplementation platform('org.junit:junit-bom:5.10.0')
    testImplementation 'org.junit.jupiter:junit-jupiter'
}

test {
    useJUnitPlatform()
}

publishing {
    repositories {
        maven {
            name = "maven.msla.io"
            url = "https://maven.msla.io/repository"
            credentials {
                username = System.getProperty("msla.maven.user")
                password = System.getProperty("msla.maven.password")
            }
        }
    }

    publications {
        mavenJava(MavenPublication) {
            from components.java
            pom {
                name = 'gerber-lib'
                description = 'Gerber processing library'
                url = 'https://msla.io/'
                licenses {
                    license {
                        name = 'The Apache License, Version 2.0'
                        url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }
                developers {
                    developer {
                        id = 'futurelink'
                        name = 'Denis Pavlov'
                        email = 'futurelink.vl@gmail.com'
                    }
                }
                scm {
                    connection = 'scm:git:git://github.com:futurelink/msla-imaging.git'
                    developerConnection = 'scm:git:ssh://github.com:futurelink/msla-imaging.git'
                    url = "https://github.com/futurelink/msla-imaging"
                }
            }
        }
    }
}
